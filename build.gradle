buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId 'pw.thedrhax.mosmetro'
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 51
        versionName "1.7.3"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile new File(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            useProguard false
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }
    }

    sourceSets {
        // Test sources.
        test.java.srcDir 'src/test/java'
        androidTest.java.srcDir 'src/androidTest/java'

        main {
            manifest.srcFile 'AndroidManifest.xml'
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            java {
                srcDir 'src'
                exclude 'androidTest/**'
                exclude 'test/**'
            }
        }

        lintOptions {
            abortOnError false
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    // Runtime dependencies.
    compile 'com.android.support:support-annotations:25.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'org.jsoup:jsoup:1.8.3'
    compile ('com.googlecode.json-simple:json-simple:1.1.1') {
        exclude module: 'junit'
    }

    // Test dependencies.
    testCompile 'junit:junit:4.12'

    // Needed because there is a conflict in 'com.android.support.test:runner' package.
    // Here we explicitly define the version for the 'androidTest' sources build variant.
    androidTestCompile 'com.android.support:support-annotations:25.1.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
}
