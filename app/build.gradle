apply plugin: 'com.android.application'
apply plugin: 'androidsvgdrawable'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId 'pw.thedrhax.mosmetro'
        minSdkVersion 14
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 25
        versionCode 77
        versionName "2.3.2"

        // Jenkins build info
        def branch_name = System.getenv("BRANCH_NAME") ?: "play"
        buildConfigField "String", "BRANCH_NAME", "\"$branch_name\""
        buildConfigField "Integer", "BUILD_NUMBER", System.getenv("BUILD_NUMBER") ?: "0"
        resValue "string", "build_branch_name", "\"$branch_name\""

        // ACRA Config
        buildConfigField "String", "TRACEPOT_ID", "\"7c36f095\""

        // GitHub Pages
        buildConfigField "String", "\$GITHUB", "\"https://mosmetro-android.github.io\""
        buildConfigField "String", "NEWS_URL", "\$GITHUB + \"/news.json\""
        buildConfigField "String", "API_URL_SOURCE", "\$GITHUB + \"/base-url\""

        // Default stat URL
        buildConfigField "String", "API_URL_DEFAULT", "\"https://mosmetro.duckdns.org\""

        // Relative URLs for stat server
        buildConfigField "String", "API_REL_STATISTICS", "\"/api/v2/stats\""
        buildConfigField "String", "API_REL_BRANCHES", "\"/api/v1/branches.php\""
        buildConfigField "String", "API_REL_DOWNLOAD", "\"/api/v2/download\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    if (keystorePropertiesFile.exists()) {
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs {
            release {
                storeFile new File(keystoreProperties['RELEASE_STORE_FILE'])
                storePassword keystoreProperties['RELEASE_STORE_PASSWORD']
                keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
                keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    sourceSets {
        main {
            res.srcDirs = ['src/main/res', 'build/generated/res/main']
            jniLibs.srcDirs = ['libs']
            java {
                srcDir 'src'
            }
        }
    }

    compileOptions { // For ACRA
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }
}

import fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask
task svgToDrawablePng(type: SvgDrawableTask) {
    from = files('src/main/res-svg/drawable')
    to = file('build/generated/res/main')
    createMissingDirectories = true
    overwriteMode = 'ifModified'
    targetedDensities = ['ldpi', 'hdpi', 'mdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'drawable'
}

dependencies {
    // Runtime dependencies.
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.work:work-runtime:2.4.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.13'
    implementation 'org.jsoup:jsoup:1.8.3'
    implementation ('com.googlecode.json-simple:json-simple:1.1.1') {
        exclude module: 'junit'
    }
    implementation 'com.edmodo:rangebar:1.0.0'
    implementation 'dnsjava:dnsjava:2.1.9'
    implementation 'com.jayway.jsonpath:json-path:2.6.0'
    implementation 'com.github.topjohnwu.libsu:core:3.1.2'

    // ACRA
    def acraVersion = '5.7.0'
    implementation "ch.acra:acra-core:$acraVersion"
    implementation "ch.acra:acra-http:$acraVersion"
}

/*
 * Compatibility with Eclipse and Visual Studio Code
 */

apply plugin: 'com.greensopinion.gradle-android-eclipse'
apply plugin: 'eclipse'

buildscript {
    dependencies {
        classpath fileTree(dir: 'libs', includes: ['gradle-android-eclipse-1.1.1.jar'])
    }
}

// https://github.com/greensopinion/gradle-android-eclipse/issues/9
configurations {
    eclipseConfig.extendsFrom implementation
    eclipseTestConfig.extendsFrom testImplementation
}

eclipse {
    classpath {
        plusConfigurations += [
            configurations.compile,
            configurations.testCompile,
            configurations.eclipseConfig,
            configurations.eclipseTestConfig,
        ]
        downloadSources = true
    }
}